# MRChemConfig.cmake
# ---------------------
#
# MRChem cmake module.
# This module sets the following variables in your project:
#
# ::
#
#   MRChem_FOUND - true if MRChem and all required components found on the system
#   MRChem_VERSION - MRChem version in format Major.Minor.Release
#   MRChem_INCLUDE_DIRS - Directory where the MRChem headers are located.
#   MRChem_INCLUDE_DIR - same as DIRS
#   MRChem_DEFINITIONS: Definitions necessary to use MRChem, namely USING_MRChem.
#   MRChem_LIBRARIES - MRChem library to link against.
#   MRChem_LIBRARY - same as LIBRARIES
#
#
# Exported targets:
#
# ::
#
# If MRChem is found, this module defines the following :prop_tgt:`IMPORTED`
# target. ::
#
#   MRChem::mrchem - the main MRChem library with header & defs attached.
#
#
# Suggested usage:
#
# ::
#
#   find_package(MRChem)
#   find_package(MRChem 1.1.7 CONFIG REQUIRED)
#
#
# The following variables can be set to guide the search for this package:
#
# ::
#
#   MRChem_DIR - CMake variable, set to directory containing this Config file
#   CMAKE_PREFIX_PATH - CMake variable, set to root directory of this package
#   PATH - environment variable, set to bin directory of this package
#   CMAKE_DISABLE_FIND_PACKAGE_MRChem - CMake variable, disables
#       find_package(MRChem) perhaps to force internal build

@PACKAGE_INIT@

set(PN MRChem)

# find includes
unset(_temp_h CACHE)
find_path(_temp_h
  NAMES MRChem/mrchem_declarations.h
  PATHS ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_INCLUDEDIR@
  NO_DEFAULT_PATH)
if(_temp_h)
  set(${PN}_INCLUDE_DIR "${_temp_h}")
  set(${PN}_INCLUDE_DIRS ${${PN}_INCLUDE_DIR})
else()
  set(${PN}_FOUND 0)
  if(NOT CMAKE_REQUIRED_QUIET)
    message(STATUS "${PN}Config missing component: header (${PN}: ${_temp_h})")
  endif()
endif()

# find library
set(_hold_library_suffixes ${CMAKE_FIND_LIBRARY_SUFFIXES})
set(CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_SHARED_LIBRARY_SUFFIX})
unset(_temp CACHE)
find_library(_temp
  NAMES mrchem
  PATHS ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@
  NO_DEFAULT_PATH)
if(_temp)
  set(${PN}_LIBRARY "${_temp}")
  set(${PN}_FOUND 1)
else()
  set(${PN}_FOUND 0)
  if(NOT CMAKE_REQUIRED_QUIET)
    message(STATUS "${PN}Config missing component: library (${PN}: ${_temp})")
  endif()
endif()
set(CMAKE_FIND_LIBRARY_SUFFIXES ${_hold_library_suffixes})
set(${PN}_LIBRARIES ${${PN}_LIBRARY})
set(${PN}_DEFINITIONS USING_${PN})

check_required_components(${PN})

#-----------------------------------------------------------------------------
# Don't include targets if this file is being picked up by another
# project which has already built this as a subproject
#-----------------------------------------------------------------------------
if(NOT TARGET ${PN}::mrchem)
  get_filename_component(_fext ${${PN}_LIBRARY} EXT)
  include("${CMAKE_CURRENT_LIST_DIR}/${PN}Targets.cmake")

  find_package(Eigen3 3.4 CONFIG REQUIRED)

  get_target_property(MRCHEM_HAS_OMP MRChem::mrchem MRCHEM_HAS_OMP)
  if(MRCHEM_HAS_OMP)
    find_package(OpenMP REQUIRED QUIET COMPONENTS CXX)
    message(STATUS "MRChem built with OpenMP support (found version \"${OpenMP_CXX_VERSION}\")")
    target_link_libraries(MRChem::mrchem INTERFACE OpenMP::OpenMP_CXX)
  endif()

  get_target_property(MRCHEM_HAS_MPI MRChem::mrchem MRCHEM_HAS_MPI)
  if(MRCHEM_HAS_MPI)
    find_package(MPI REQUIRED QUIET COMPONENTS CXX)
    message(STATUS "MRChem built with MPI support (found version \"${MPI_CXX_VERSION}\")")
    target_link_libraries(MRChem::mrchem INTERFACE MPI::MPI_CXX)
  endif()
endif()

